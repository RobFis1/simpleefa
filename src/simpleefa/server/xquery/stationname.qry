declare namespace functx = 'http://www.functx.com'; 
declare function functx:trim($arg as xs:string? ) as xs:string {
	fn:replace(fn:replace($arg,'(\\s|[ ]|[,.!:;]|[n][b][s][p][;])+$',''),'^(\\s|[ ]|[,.!:;]|[n][b][s][p][;])+','')
};
declare function functx:repeat-string($stringToRepeat as xs:string?,$count as xs:integer) as xs:string { 
	string-join((for $i in 1 to $count return $stringToRepeat),'')
};
declare function functx:pad-integer-to-length ($integerToPad as xs:anyAtomicType?,$length as xs:integer )  as xs:string {
	if ($length < string-length(string($integerToPad)))
	then error(xs:QName('functx:Integer_Longer_Than_Length'))
	else concat
		(functx:repeat-string(
			'0',$length - string-length(string($integerToPad))),
		string($integerToPad))
	} ;
declare function functx:time ($hour as xs:anyAtomicType,$minute as xs:anyAtomicType,$second as xs:anyAtomicType)  as xs:time {
 xs:time(
  concat(
    functx:pad-integer-to-length(xs:integer($hour),2),':',
    functx:pad-integer-to-length(xs:integer($minute),2),':',
	 functx:pad-integer-to-length(xs:integer($second),2)))
} ;
declare variable $doc external;
declare variable $input external;
declare variable $limit external;
declare variable $x_cor external;
declare variable $y_cor external;
for $req in $doc//itdDepartureMonitorRequest 
let $sortet_loc := for $loc in $req/itdOdv/itdOdvName/odvNameElem order by number(data($loc/@matchQuality)) descending return $loc
 return 
<possible_stations>
<for_input>
	{$input}
</for_input>
{
 
 for $loc in $sortet_loc[position() lt ($limit+1)] 
	let $match := data($loc/@matchQuality)
	let $id := data($loc/@id)
	let $x := data($loc/@x)
	let $y := data($loc/@y)
	let $stationname := data($loc/@objectName)
	let $locationname := data($loc/@locality)
	let $name := $loc/child::text()
	let $c := fn:count($doc//itdDepartureMonitorRequest/itdOdv/itdOdvName/odvNameElem)
	order by number($match) descending
	return 
		<station id='{$id}' matching='{if ($c>1) then $match else 1000}'>
			{$name}
			<location_name>
				{$locationname}
			</location_name>
			<station_name>
				{$stationname}
			</station_name>
			<position lng='{xs:decimal($x) * $x_cor}' lat='{xs:decimal($y) * $y_cor}' />
		</station>
}
</possible_stations>